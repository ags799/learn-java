buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.7'
  }
}

plugins {
  id 'nebula.dependency-recommender' version '4.1.2'
}

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'net.ltgt.apt'
apply plugin: 'idea'

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyRecommendations {
  propertiesFile file: file('versions.props')
}

dependencies {
  apt 'org.immutables:value'
  compileOnly 'org.immutables:value'
  compile (
    'com.amazonaws:aws-lambda-java-core',
    'com.amazonaws:aws-lambda-java-log4j',
    'com.fasterxml.jackson.core:jackson-core',
    'com.fasterxml.jackson.core:jackson-databind',
    'com.fasterxml.jackson.core:jackson-annotations',
    'com.google.guava:guava',
    'com.amazonaws.serverless:aws-serverless-java-container-jersey',
    'org.glassfish.jersey.media:jersey-media-json-jackson',
  )
  testCompile (
      'junit:junit',
      'org.assertj:assertj-core'
  )
}

checkstyle {
  configFile file('checkstyle.xml')
  maxWarnings = 0
  toolVersion '7.6.1'
}

task deployable(type: Zip, group: 'deployment') {
  // The path to this file must be set in the serverless.yml file under package.artifact.
  baseName = "hello"
  from compileJava
  from processResources
  into('lib') {
    from configurations.runtime
  }
}

task deploy(type: Exec, group: 'deployment', dependsOn: 'deployable') {
  commandLine 'serverless', 'deploy'
}
