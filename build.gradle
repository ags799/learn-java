plugins {
  id 'nebula.dependency-recommender' version '4.1.2'
}

apply plugin: 'java'
apply plugin: 'checkstyle'

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyRecommendations {
  propertiesFile file: file('versions.props')
}

dependencies {
  compile (
    'com.amazonaws:aws-lambda-java-core',
    'com.amazonaws:aws-lambda-java-log4j',
    'com.fasterxml.jackson.core:jackson-core',
    'com.fasterxml.jackson.core:jackson-databind',
    'com.fasterxml.jackson.core:jackson-annotations'
  )
}

checkstyle {
  configFile file('checkstyle.xml')
  maxWarnings = 0
  toolVersion '7.6.1'
}

task deployable(type: Zip) {
  // The path to this file must be set in the serverless.yml file under package.artifact.
  baseName = "hello"
  from compileJava
  from processResources
  into('lib') {
    from configurations.runtime
  }
}

task deploy(type: Exec, group: 'deployment', dependsOn: 'deployable') {
  commandLine 'serverless', 'deploy'
}
